!            *****************************
!            **  PROGRAM KellerSegelVF  **
!            *****************************
!******************************************************************************
!     *-!!!!!!!!!@@@@@!!@@@@@!!@@@@@!!@@@@@!!!!!!!!!!!-*
!     *-!!!!!!!!!!!!!@!!@!!!@!!!!!!@!!@!!!@!!!!!!!!!!!-*
!     *-!!!!!!!!!@@@@@!!@!!!@!!@@@@@!!@!!!@!!!!!!!!!!!-*
!     *-!!!!!!!!!@!!!!!!@!!!@!!@@!!!!!@!!!@!!!!!!!!!!!-*
!     *-!!!!!!!!!@@@@@!!@@@@@!!@@@@@!!@@@@@!!!!!!!!!!!-*
!------------------------------------------------------------
!     * Programme resoud le système de chimiotaxie anisotropic
!       sur un domaine qlq par la methode des DDFV:
!
!      Probleme  traité est le suivant : 
!      Dt U - Div( S(x) nabla A(u) ) + div(S(x) X(u) nabla V) = 0    dans omega
!      Dt V - Div( D(x) nabla V) = a U-b V
!               DA(u).n = D V.n=0        sur gamma_1
!               u=udb, v=vdb             sur gamma_2
!
!----------------
! La méthode utilise le maillage dual de Vernoi. 
!******************************************************************************
PROGRAM KellerSegelVF
  
  !========
  ! Modules
  !========
  USE longr
  USE parmmage
  USE imprime
  USE fsource
  USE plotvtkmod

  IMPLICIT NONE
  !==========================
  ! Declaration des tableaux
  !==========================
  TYPE(MatCreux)       :: AU, AV
  !==================================
  ! Declaration des variables locales
  !==================================
  INTEGER         :: jt, i, j, is,ii,niter,y
  REAL(kind=long), DIMENSION(:),   Pointer  :: Uold, U, Vold, V, Uexacte
  REAL(kind = long)                         :: Tempsactuel
  REAL(kind=long), DIMENSION(:),ALLOCATABLE :: NormL1U, NormL1V
  CHARACTER(20) :: chaine
  real :: start, finish
  !===================
  ! Debut du programme
  !===================
  call cpu_time(start)
  prefix = 'KelSeg '
  !
  !! coef devant la derivee en temps
  !! si CFDT = 0, pb stationnaire
  pi = 4.D0*atan(1.)
  !!
  CALL init                    !* Initialisation

  CALL maillage                !* lecture du maillage

  CALL meshtools               !* Mise à jour du maillage
  !------------------------------------
  ! Nombre d'inconnues dans le probleme
  !------------------------------------
  IF (ChoixCdtBord == Neumann) NsInt = NbS
  NbInc = NsInt + Nbt
  print*,'NbInc = ',NbInc
  ALLOCATE(Uold(NbInc), U(NbInc), Vold(NbInc), V(NbInc), Uexacte(NbInc))
  ALLOCATE( Gb(Nbs + Nseg) ) ! Gb est de taille  Nbs - NsInt
  !---------------------------------------------
  ! Condition initiale et conditions aux limites
  !---------------------------------------------
  affichage = 0
  CALL Conditioninitiale(U,V,NbInc)
  write(*,*)'cdt initiale ok'
  !---------------------------------------
  ! Allocation de la structure des données
  !---------------------------------------
  IF (Choixdt == 0) dt = 0.1*(maxval(Msig))**2
  print*,'dt    =',dt
  CALL matrixinitDDFV(AU)
  CALL matrixinitDDFV(AV)
  !Uold = U
  !Vold = V
  !----------------------------------------------
  ! Assemblagle du second membre de AU%F physique
  !----------------------------------------------
  !CALL scmem(AU, choixpb, dt)
  !-----------------------------------------
  ! Coefficients de transmissibilités pour u
  !-----------------------------------------
  CALL tenseur(Choixanisu)
  CALL coefftransm
  !-----------------------------------------
  ! Coefficients de transmissibilités pour v
  !-----------------------------------------
  CALL tenseurv(Choixanis)
  CALL coefftransmv
  !! -------------
  !! Temps initial 
  !! -------------
  Tempsactuel = 0.D0
  CALL plot_vtk (U,'U00000','U')
  CALL plot_vtk (V,'V00000','V')
  !
  Allocate(NormL1U(nbitertemps),NormL1V(nbitertemps))
  NormL1U = 0.D0
  NormL1V = 0.D0
  !
  !! =============================================
  !! %%%%%%%%%%%%%% Boucle en Temps %%%%%%%%%%%%%%
  !! =============================================
  niter = 1
  DO WHILE (niter  <= nbitertemps)  
     print*,' Boucle en TEMPS iter = ', niter     
     print*,' Max solution calculee U   : ', MAXVAL(U)
     print*,' Min solution calculee U   : ', MINVAL(U)
     print*,' Max solution calculee V   : ', MAXVAL(V)
     print*,' Min solution calculee V   : ', MINVAL(V)
     Tempsactuel = Tempsactuel + dt
     ! -------------------------------------
     ! Norme L1 de U et V à chaque iteration
     ! -------------------------------------
     DO is = 1, NsInt
        NormL1U(niter) = NormL1U(niter) + AireDsommet(is)*U(is)/2
        NormL1V(niter) = NormL1V(niter) + AireDsommet(is)*V(is)/2
      END DO
     !
     DO jt = 1, Nbt
        ii = jt + NsInt
        NormL1U(niter) = NormL1U(niter) + AireK(jt)*U(ii)/2
        NormL1V(niter) = NormL1V(niter) + AireK(jt)*V(ii)/2
     END DO
     !
     !IF (niter == 2) epsilon = 1.D-12
     !IF (MINVAL(U)>=-1.D-12 .AND. MINVAL(U)< 0) U = 0.D0
     !IF (MINVAL(V)>=-1.D-12 .AND. MINVAL(V)< 0) V = 0.D0
     Uold = U
     Vold = V
     !----------------------------------------------
     ! Assemblagle du second membre de AU%F physique
     !----------------------------------------------
     !CALL scmem(AU, choixpb, Tempsactuel) 
     !----------------------------------------------
     CALL ubord(Choixgb,Tempsactuel)

     !--------------------------------------------------
     ! Resolution du systeme nonlineaire pour calculer U
     !--------------------------------------------------
     ! Methode de Newton
     CALL NewtonvDDFV(AV,Vold,V,U,NbInc,choixpb,Tempsactuel)
     CALL NewtonuDDFV(AU,Uold,U,V,NbInc,choixpb,Tempsactuel)
     !------------------------------------------
     ! Erreur L^infinie entre U approchee et Uex
     !------------------------------------------
     DO is = 1, NsInt
        Uexacte(is) = gbord(Tempsactuel,coordS(1,is), CoordS(2,is), Choixgb) ! solution exacte
     END DO
     DO jt = 1, Nbt
        Uexacte(jt+ NsInt) = gbord(Tempsactuel,CoordK(1,jt), CoordK(2,jt), Choixgb) ! solution exacte
     END DO
     WRITE(* ,*)' Erreur infinie U-Uexacte =  : ', MAXVAL(ABS(U - Uexacte))
     WRITE(* ,*)' Erreur infinie relative  =  : ', MAXVAL(ABS(U - Uexacte))/MAXVAL(ABS(Uexacte))

     !-------------------
     ! Affichage resultat
     !-------------------
     ! 
     IF (mod(niter+affichage,Nb_iter_visit) == 0) then
        Select case ( TypecondinitialeU )
           !
        case (3)
           write(chaine,'(i5.5)')niter+affichage
           CALL plot_vtk (U,'U'//trim(chaine),'U')
           CALL plot_vtk (V,'V'//trim(chaine),'V')
        case default
           write(chaine,'(i5.5)')niter
           CALL plot_vtk (U,'U'//trim(chaine),'U')
           CALL plot_vtk (V,'V'//trim(chaine),'V')
        END Select
     END IF
        
     niter = niter + 1
     !
  END DO
  !!
  CALL plot_vtk (U,'Ufinal','U')
  CALL plot_vtk (V,'Vfinal','V')
  !!
  close (cdtinitialeU)
  close (cdtinitialeV)
  OPEN (cdtinitialeU,file = 'cdtinitialeU',status='unknown')
  OPEN (cdtinitialeV,file = 'cdtinitialeV',status='unknown')
  DO is = 1, NbInc 
     WRITE(cdtinitialeU,*) U(is)
     WRITE(cdtinitialeV,*) V(is)
  END DO
  !!
  CALL prvari (uprint, 'affichage       : ', niter+affichage-1)
  !!
  DO i = 1, nbitertemps 
     WRITE(NormU,211) (i-1)*dt, NormL1U(i)
     WRITE(NormV,211) (i-1)*dt, NormL1V(i)
  END DO

!!$  !------------------------------------------
!!$  ! Erreur L^infinie entre U approchée et Uex
!!$  !-------------------------------------------
!!$  DO is = 1, NsInt
!!$     Uexacte(is) = gbord(dt,coordS(1,is),  CoordS(2,is), Choixgb) ! solution exacte
!!$  END DO
!!$  DO jt = 1, Nbt
!!$     Uexacte(jt+ NsInt) = gbord(dt,CoordK(1,jt),  CoordK(2,jt), Choixgb) ! solution exacte
!!$  END DO
!!$  WRITE(* ,*)' Erreur infinie V-Uexacte =  : ', MAXVAL(ABS(V - Uexacte))
!!$  WRITE(* ,*)' Erreur infinie relative  =  : ', MAXVAL(ABS(V - Uexacte))/MAXVAL(ABS(Uexacte))
100 FORMAT(10(E10.3,2x))
200 FORMAT(I6,2x, 6(E10.3,2x))
211 FORMAT (2(E12.6,2x))
  close (uprint)
  close (usave)
  close (upoints)
  Deallocate(TKL,TKeLe,eta,AireD,AireDsommet,U,Uold,Gb)
  Deallocate(vTKL,vTKeLe,veta,NormL1U,NormL1V)
  Deallocate(AireK,NsigK,NsigeKe,Msig,Msige)
  call cpu_time(finish)
  print*,'finish',start,finish,finish-start
  PRINT '("Fin du travail Keller-Segel en ",f10.3," secondes.")',finish-start
END PROGRAM KellerSegelVF




